	.TITLE	PINCON
	.RADIX	16
	.CSECT
	.LIST	MEB
;
;GLOBALS
;
	.GLOBL	PINCOIN		;ENTRY POINTS
	.GLOBL	COINST,POSTSL,SLMTIM,BLCOIN,BRCOIN,BHRCOIN,BHLCOIN
	.GLOBL	MINC,CLIT,INDEX
	.GLOBL	SWTIN,NEWSWT
	.GLOBL	SOLC1
	.GLOBL	PWRON
	.GLOBL	FCOINS		;FEATURE ROM COIN MODES
	.GLOBL	COP1,COP2
LCMTR	=6
RCMTR	=0C
TCMTR	=12
;ENTERED EVERY 4 MILLISECONDS
;
PINCOIN:LDX	I,1		;INDEX
DETECT:	LDAA	D,SWTIN		;GET COIN BIT WORD
	ASLA
	ASLA
	ASLA
	ASLA			;IF CARRY=1 A COIN IS IN THE CHUTE
	DEX
	BEQ	40$
	ASLA
40$:	INX
	LDAB	X,COINST	;STATUS WORD
	ANDB	I,31.		;COIN ON DOWN-COUNTER ONLY
	BCC	8$
	DECB
	BNE	1$		;DON'T DECR BEYOND 0
	INCB
1$:	STAB	X,COINST	;SAVE STATUS
	LDAB	D,SWTIN+6	;READ TILT SWITCH
	LSRB			;CHECK SWITCH
	BCS	3$		;SLAM ON!
	LDAB	D,SLMTIM	;CHECK TIMER
	BEQ	5$		;IF ZERO, OK
	DECB			;ELSE DECR ONCE PER INTERRUPT PER MECH
	BRA	4$		;KEEP STATUS CLEAR
3$:	LDAB	I,0F0		;TIMER=120/8=15 FRAMES 1/4 SECOND
4$:	STAB	D,SLMTIM
	CLR	X,COINST
	CLR	X,POSTSL
5$:	CLC			;NO VALID COIN
6$:	LDAB	X,POSTSL	;CHECK POST SLAM COUNTER
	BEQ	12$		;ZERO, RETURN
	DEC	X,POSTSL	;ELSE DECR
	BNE	12$		;NOT DONE, RETURN
	SEC			;A VALID COIN!!
	BRA	12$
8$:	CMPB	I,26.		;HAS COIN BEEN ON 5 TIMES?
	BLS	10$		;YES
9$:	LDAB	I,31.		;ELSE SET TO STARTING COUNT
	BRA	1$
10$:	LDAB	X,COINST	;GET STATUS
	ADDB	I,32.		;INC COIN GONE UP CTR
	BCC	1$		;NO WRAP
	BEQ	9$		;RESET IF "STRING-TIMER" SET
;
;ABOVE WILL IGNORE ANY COIN PRESENT FOR 32 4 MS PERIODS
;
	CMPB	I,26.		;COIN HAS BEEN GONE 32 MS, HOW LONG NOW.
	BGT	9$		;NOT LONG ENOUGH-RESET CNTR
	LDAB	I,31.
	STAB	X,COINST
;
;NOW STILL HAVE TO WAIT 1/2 SECOND FOR POST SLAMS
;
	TST	X,POSTSL	;IS TIMER OCCUPIED (CLEARS CARRY)
	BEQ	11$		;NO, VACANT
	SEC			;ACCEPT PREVIOUS OCCUPANT
11$:	LDAB	I,78		;TENTATIVE COIN
	STAB	X,POSTSL
;CARRY CONTAINS COIN INFORMATION
12$:	BCC	ENDLOOP		;NO COIN THIS TIME
	SEI
	LDAA	I,80		;GUESS RIGHT COIN
	DEX			;TEST WHICH MECH
	BNE	LFTCOIN		;MECH ZERO
	INC	E,BRCOIN	;INCR COIN COUNTER
	ORAA	D,SOLC1		;COUNT RIGHT COIN
	STAA	D,SOLC1
	LDAA	I,RCMTR
	JSR	MINC		;COUNT LEFT COIN
	BSR	BCOIN		;COUNT TOTAL COINS, RETURN
	LDX	I,1		;RIGHT COIN INDEX
	BRA	PVCOIN		;ADD TO CREDITS
;
;THIS IS A HIDDEN SUBROUTINE, HIDDEN TO SAVE MEMORY
;
BCOIN:	LDAA	I,TCMTR		;TOTAL COIN METER UPDATE
	JMP	MINC		;AND RETURN
;
;THIS VECTOR ACTUALLY SAVES ONE BYTE
;
TODETECT:BRA	DETECT
;
;
LFTCOIN:INC	E,BLCOIN	;INCR COIN COUNTER
	LSRA
	ORAA	D,SOLC1		;COUNT LEFT COIN
	STAA	D,SOLC1
	LDAA	I,LCMTR
	JSR	MINC		;COUNT RIGHT COIN
	BSR	BCOIN		;COUNT TOTAL COINS
	LDX	I,0
;
;	FALL INTO THE ROUTINE TO PROCESS VALID COINS (PVCOIN)
;
	.PAGE
;COIN MODE OPTION TABLE
;							COIN/CREDIT
;	SWITCHES					 SEQUENCE
;	  43210		COINS		CREDITS		1    2    3
;	--------	-----		-------		-----------
;
;	  00010		  1		   1		1    
;	  00100		  1		   2		2
;	  00110		  1		   3		3
;	  01000		  1		   4		4
;	  01010		  1		   5		5
;	  01100		  1		   6		6
;	  01110		  1		   7		7
;	  00000		  1		   8		8
;	  10000		  1		   9		9
;	  10001		  1		  10	       10
;	  10010		  1		  11	       11
;	  10011		  1		  12	       12
;	* 0100		  1		  13	       13
;	  10101		  1		  14	       14
;	* 10110		  1		  15	       15
;	  00001		  2		   1		0    1
;	  10111		  2		   2		0    2
;	  00011		  2		   3		1    2
;	* 11000		  2		   3		     3
;	  11001		  2		   4		     4
;	  00101		  2		   5		2    3
;	  00111		  2		   7		3    4
;	  01001		  2		   9		4    5
;	  01011		  2		  11		5    6
;	  01101		  2		  13		6    7
;	  01111		  2		  15		7    8
;	* 11010		  3		   1		     0    1
;	  11011		  3		   2		0    1    1
;	* 11100		  3		   2		0    0    2
;	* 11101		  3		   3		0    0    3
;	* 11110		  3		   4		0    2    2
;	* 11111		  3		   4		0    0    4
;
;
; *MEANS SUGGESTED NUMBER AND ORDER OF
;  COIN MODES FOR FEATURE ROM
;
;
	.PAGE
;
;"S" BIT SET POSSIBILITIES
;
;FORMAT IS 8 BITS
;76543210
;AABBDDDD
;
;DDDD REPRESENTS BASE 1/2 CREDITS PER COIN
;BB+1 REPRESENTS EXTRA CREDITS PER SPECIAL COIN
;AA   SPECIFIES SPECIAL COINS AS
;
;	IF AA=00 THEN NO SPECIAL COINS.  USE BBDDDD AS 1/2 CREDITS
;		 RANGE IS 0 TO 31 & 1/2 CREDITS PER COIN
;	IF AA=01 THEN EVERY 2ND COIN IS SPECIAL.  USE DDDD FOR EACH
;		 COIN PLUS BB+1 FOR EVERY 2ND COIN.
;EXAMPLES:
;	1.   DDDD=0000(0) BB-00(0)
;	     2 COINS, 1 CREDIT, 1ST=0, 2ND=1(0+0+1)
;	2.   DDDD=1100(12 1/2 CREDITS=6 CREDITS)
;	     BB=10=2 CREDITS
;	     2 COINS, 15 CREDITS, 1ST=6, 2ND=9(6+2+1)
;
;	IF AA=10 THEN EVERY 2ND AND 3RD COIN IS SPECIAL.  USE DDDD AS 1/2
;		 CREDITS PER COIN PLUS BB+1 FOR THE 2ND AND 3RD COINS.
;EXAMPLES:
;	1.   DDDD=0000(0) BB=00(0)
;	     3 COINS, 2 CREDITS, 1ST=0, 2ND=1(0+0+1), 3RD=1(0+0+1)
;	2.   DDDD=0101(5 1/2 CREDITS=2 1/2 CREDITS)
;	     BB=11=3 CREDITS
;	     3 COINS, 15 CREDITS, 1ST=2, 2ND=7(3+3+1), 3RD=6(2+3+1)
;
;	IF AA=11 THEN EVERY 3RD COIN IS SPECIAL.  USE DDDD AS 1/2 CREDITS
;		 PER COIN PLUS BB+1 FOR THE 3RD COIN
;EXAMPLES:
;	1.   DDDD=0000(0) BB=00(0)
;	     3 COINS, 1 CREDITS, 1ST=0, 2ND=0, 3RD=1(0+0+1)
;	2.   DDDD=0100(4 1/2 CREDITS=2 CREDITS)
;	     BB=10=2 CREDITS
;	     3 COINS, 9 CREDITS, 1ST=2, 2ND=2, 3RD=5(2+2+1)
	.PAGE
;
;"S" BIT (4) SET TABLE FOR SOUPY
;						  SEQ
;
;	AA	BB	DDDD	CN	CR	1  2  3		CCCC
;	00	01	0010	 1	 9	9		  0
;	00	01	0100	 1	10     10		  1
;	00	01	0110	 1	11     11		  2
;	00	01	1000	 1	12     12		  3
;	00	01	1010	 1	13     13		  4
;	00	01	1100	 1	14     14		  5
;	00	01	1110	 1	15     15		  6
;	01	01	0000	 2	 2	0  2		  7
;	01	10	0000	 2	 3	0  3		  8
;	01	11	0000	 2	 4	0  4		  9
;	11	00	0000	 3	 1	0  0  1		 10
;	10	00	0000	 3	 2	0  1  1		 11
;	11	01	0000	 3	 2	0  0  2		 12
;	11	10	0000	 3	 3	0  0  3		 13
;	10	01	0000	 3	 4	0  2  2		 14
;	11	11	0000	 3	 4	0  0  4		 15
;
	.PAGE
;
;	X=1 FOR RIGHT COINS
;	X=0 FOR LEFT COINS
;
;
;PROCESS A VALID COIN
;
PVCOIN:
CKCOIN:	DEX			;TEST X
	BNE	20$		;ITS THE LEFT COIN
;
; CHECK IF RIGHT COIN MODE=LEFT CON MODE
;
	LDAB	E,BRCOIN	;RIGHT COIN COUNT
	LDAA	E,COP2		;RIGHT COIN MODE
	EORA	E,COP1		;CHECK WITH LEFT COIN MODE
	ANDA	I,1F
	BEQ	5$		;THEY ARE THE SAME
	LDAA	E,COP2		;RESTORE RIGHT COIN MODE
	BRA	30$
5$:	ANDB	I,0F		;COINS IN RIGHT MECH
	BEQ	RESX
10$:	INC	E,BLCOIN	;TRANSFER TO LEFT MECH
	DEC	E,BRCOIN	;THIS IS REENTRANT!
	DECB
	BNE	10$
	DEX			;MAKE X=-1 FOR A LEFT COIN
;
; NOW PROCESS RIGHT COINS AS LEFT COINS
;
20$:	LDAA	E,COP1		;LEFT COIN OPTIONS
	LDAB	E,BLCOIN
30$:	ANDB	I,0F		;MASK TO Y BITS OF COINS
	BNE	DECODE		;DO IT-GOT MONEY!
RESX:	INX			;RESTORE X
ENDLOOP:DEX			;GO TO NEXT COIN
	BEQ	TODETECT	;DO LEFT COIN
	RTS			;DONE, EXIT
DECODE:	PSHB			;SAVE B
	CLRB			;GUESS X=0
	INX			;RESTORE X REGISTER
	BNE	20$
	INCB			;NO X=1
20$:	ANDA	I,1F		;MASK TO OPTIONS
	BITA	I,10		;TABLE "S" REQUEST?
	BEQ	GOTOPT		;GIVE CREDIT
	LDX	I,FCOINS-10	;USE FEATURING TABLE
30$:	JSR	INDEX		;POINT INTO TABLE
	LDAA	X,0		;GET TABLE CONTENTS
GOTOPT:	LDX	I,0
	TSTB
	BNE	20$		;RESTORE X REGISTER
	INX
20$:	PULB			;RESTORE COIN COUNT
;
;DECODE OPTION
;	A=OPTION
;	B=NUMBER OF COINS
;	X=LEFT/RIGHT POINTER
;
;
;DECODE AA BITS 6 AND 7
;
;
	BITA	I,0C0		;AA=0?
	BEQ	AA0		;YES
	BITA	I,080		;AA=2 OR 3
	BNE	AA23		;ELSE AA=1
;
;AA=1, EVERY 2ND COIN IS SPECIAL
;
AA1:	CMPB	I,1		;TEST COIN COUNT
	BGT	DEC2T		;SPECIAL COIN
NOTSPEC:ANDA	I,0F		;MASK TO DDDD BITS
NOTSP2:	BNE	ADDCRED		;ADD IT GOT SOMETHING
	BRA	ENDLOOP		;NO CREDIT NOW
;
;AA=2 OR 3
;
AA23:	BITA	I,40		;TEST IF 2
	BNE	AA3		;ITS 3
;
;AA=2
;	2ND AND 3RD COINS ARE SPECIAL
;
AA2:	DECB			;TEST COIN COUNT
	BEQ	NOTSPEC		;NOT SPECIAL
	CMPB	I,2
	BLT	SPECIAL		;MUST BE 2ND COIN
	BRA	DEC3T		;THIS IS THE 3RD COIN
;
;AA=0	NO SPECIAL COINS
;
AA0:
	BSR	DECCN		;CLEAR COIN COUNT
	ANDA	I,3F
	BNE	ADDCRED		;A=HALF CREDITS
	LDAA	I,10		;IF ZERO GIVE 8 CREDITS
	BRA	ADDCRED
;
;AA=3
;	THIRD COIN IS SPECIAL
;
AA3:	CMPB	I,2
	BLE	NOTSPEC		;NOT A SPECIAL COIN
DEC3T:	BSR	DECCN
DEC2T:	BSR 	DECCN		;DECR COIN COUNT
	BSR	DECCN
SPECIAL:TAB			;B=COIN OPTION
	ANDA	I,0F		;A=CREDITS PER COIN
	LSRB
	LSRB			;GET BB BONUS BITS *2
	LSRB
	ANDB	I,6		;TIMES 2 AND MASK
	ADDB	I,2		;PLUS 1 * 2
	ABA			;ADD CREDITS TO COINS
ADDCRED:DEX			;TEST WHICH COIN
	BNE	20$		;LEFT COIN
	ROR	E,BHRCOIN	;GET HALF COIN MEMORY
	ADCA	I,0		;ADD THE CARRY IN
	LSRA			;GET FULL CREDITS
	ROL	E,BHRCOIN	;GET FOR NEXT TIME
10$:	JSR	CLIT
	BRA	RESX		;DO NEXT LOOP
20$:	ROR	E,BHLCOIN	;GET HALF COIN MEMORY
	ADCA	I,0
	LSRA			;GET HALF CREDITS
	ROL	E,BHLCOIN	;SAVE HALF CREDITS
	BRA	10$
;
;DECR COIN COUNTER ALL REGS PRESERVED
;
DECCN:	DEX			;TEST X REG
	BNE	30$		;LEFT COIN
	DEC	E,BRCOIN
	BRA	40$		;EXIT
30$:	DEC	E,BLCOIN
40$:	INX			;RESTORE X
	RTS
	.BYTE 0			;FIRST 2K CHSM
	.END
                                                                                                                                                                                                                                                                                                                                                                                                                                                                          